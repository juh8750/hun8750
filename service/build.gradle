// build.gradle (Groovy DSL)

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'org.blog'
version = '1.0-SNAPSHOT'

def targetJavaVersion = 17

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }
    maven { url = 'https://repo.codemc.io/repository/maven-snapshots/' }
    maven { url = 'https://repo.dmulloy2.net/repository/public/' }
    flatDir {
        dirs 'libs'
    }
}

dependencies {

    implementation 'org.mariadb.jdbc:mariadb-java-client:2.7.3'
    implementation 'net.wesjd:anvilgui:1.10.3-SNAPSHOT'
    implementation 'redis.clients:jedis:4.4.0'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.4.0'
    compileOnly files('libs/HolographicDisplays.jar')
    compileOnly 'io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT'}


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}


processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
